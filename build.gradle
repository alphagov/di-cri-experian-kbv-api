plugins {
	id 'java-library'
	id "com.diffplug.spotless" version "6.0.4"
	id "org.sonarqube" version "3.3"
}

ext {
	dependencyVersions = [
		aws_sdk_version          : "1.12.153",
		aws_lambda_core_version  : "1.2.1",
		aws_lambda_events_version: "3.11.0",
		aws_powertools_version   : "1.9.0",
		jackson_version          : "2.13.1",
		junit                    : "5.8.2"
	]
}

repositories {
	maven {
		url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
	}
}

subprojects {

	java {
		sourceCompatibility = JavaVersion.VERSION_11
		targetCompatibility = JavaVersion.VERSION_11
	}

	repositories {
		maven {
			url 'https://gds.jfrog.io/artifactory/di-allowed-repos'
		}
	}

	configurations {
		dynamodb
		cache
		jackson
		tests
		test_runtime
		logging_runtime
		lambda
		powertools
		hibernate
	}

	dependencies {
		dynamodb "com.amazonaws:aws-java-sdk-dynamodb:${dependencyVersions.aws_sdk_version}",
				"software.amazon.awssdk:dynamodb-enhanced:2.17.116"

		cache "com.github.ben-manes.caffeine:caffeine:3.0.5"

		jackson "com.fasterxml.jackson.core:jackson-databind:${dependencyVersions.jackson_version}",
				"com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${dependencyVersions.jackson_version}",
				"org.apache.cxf:cxf-rt-frontend-jaxws:3.4.5",
				"org.apache.cxf:cxf-rt-transports-http:3.4.5",
				"javax.annotation:javax.annotation-api:1.3.2",
				"com.sun.xml.messaging.saaj:saaj-impl:1.5.3",
				"javax.el:javax.el-api:3.0.0",
				"org.glassfish:javax.el:3.0.0"


		tests "org.junit.jupiter:junit-jupiter-api:${dependencyVersions.junit}",
				"org.junit.jupiter:junit-jupiter-params:${dependencyVersions.junit}",
				"org.junit.jupiter:junit-jupiter:${dependencyVersions.junit}",
				"org.mockito:mockito-junit-jupiter:4.3.1",
				"org.mockito:mockito-inline:4.3.1",
				"org.mockito:mockito-junit-jupiter:4.1.0",
				"org.mockito:mockito-inline:4.2.0"

		test_runtime "org.junit.jupiter:junit-jupiter-engine:${dependencyVersions.junit}"

		logging_runtime 'org.apache.logging.log4j:log4j-slf4j18-impl:2.17.1',
				'com.amazonaws:aws-lambda-java-log4j2:1.5.1'

		hibernate "org.hibernate:hibernate-validator-annotation-processor:6.2.1.Final",
				"org.hibernate.validator:hibernate-validator:6.2.1.Final"


		lambda "com.amazonaws:aws-lambda-java-core:${dependencyVersions.aws_lambda_core_version}",
				"com.amazonaws:aws-lambda-java-events:${dependencyVersions.aws_lambda_events_version}"

		powertools "software.amazon.lambda:powertools-tracing:1.10.2",
				"software.amazon.lambda:powertools-parameters:1.10.2"
	}
}

tasks.named("jar") {
	manifest {
		attributes "Implementation-Title": project.name,
		"Implementation-Version": project.version
	}
}


spotless {
	java {
		target "src/main/**/*.java"
		googleJavaFormat("1.13.0").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

sonarqube {
	properties {
		property "sonar.projectKey", "alphagov_di-cri-experian-kbv-api"
		property "sonar.organization", "alphagov"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

defaultTasks 'clean', 'spotlessApply', 'spotlessGroovyGradleApply', 'build'
